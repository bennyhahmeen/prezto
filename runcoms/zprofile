#
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

#
# Browser
#

if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

#
# Editors
#

if [[ (( $+commands[emacs] )) ]]; then
  export EDITOR="$commands[emacs] -nw"
  export VISUAL="$commands[emacs] -nw"
elif [[ (( $+commands[nvim] )) ]]; then
  export EDITOR='nvim'
  export VISUAL='nvim'
elif [[ (( $+commands[vim] )) ]]; then
  export EDITOR='vim'
  export VISUAL='vim'
else
  export EDITOR='vi'
  export VISUAL='vi'
fi

export PAGER='less'

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

#
# Paths
#

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
path=(
  /usr/local/{bin,sbin}
  $path
)

[[ -d $HOME/.local/bin ]] && path=( $path $HOME/.local/bin )
[[ -d $HOME/.pyenv/bin ]] && path=( $path $HOME/.pyenv/bin )
[[ -x $HOME/.emacs.d/bin/doom ]] && path=( $path $HOME/.emacs.d/bin )

(( $+commands[pyenv] )) && eval "$(pyenv init - --no-rehash zsh)"
(( $+commands[rbenv] )) && eval "$(rbenv init - --no-rehash zsh)"
(( $+commands[direnv] )) && eval "$(direnv hook zsh)"

(( $+commands[ros] )) && path=( $path $HOME/.roswell/bin )
(( $+commands[go] )) && path=( $path $(go env GOPATH)/bin )

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

# WSL
if [[ -v WSLENV ]]; then
  export LIBGL_ALWAYS_INDIRECT=1
  export DISPLAY=$(awk '/nameserver / {print $2; exit}' /etc/resolv.conf 2>/dev/null):0
fi
